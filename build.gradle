plugins {
    id 'java'
    id "org.jetbrains.kotlin.jvm" version "1.5.31"
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group = 'me.bscal'
version = '1.0-SNAPSHOT'

sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
    main.java.srcDirs += 'src/main/java'
}

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://papermc.io/repo/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        url "https://libraries.minecraft.net"
    }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.17.1-R0.1-SNAPSHOT'
    compileOnly 'com.mojang:brigadier:1.0.18'
    implementation 'net.axay:kspigot:1.17.4'
    implementation 'com.h2database:h2:1.4.200'
    implementation 'com.zaxxer:HikariCP:5.0.0'
    // 'com.j256.ormlite:ormlite-core:5.6'
}

shadowJar {
    dependencies {
        exclude(dependency("net.axay:kspigot:1.17.4")) // Uses plugin.yml 's library to automatically include
    }
}

def targetJavaVersion = 16
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.release.set(targetJavaVersion)
    options.encoding = "UTF-8"
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "16"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "16"
}

